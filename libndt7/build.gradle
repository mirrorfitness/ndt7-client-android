apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "maven-publish"
apply plugin: "org.jlleitschuh.gradle.ktlint"

// For handling versioning internally
apply from: 'version.gradle'

def mavenVersion = libVersionInfo.getVersionString()
def mavenVersionCode = libVersionInfo.getVersionCode()

publishing {
    publications {
        release(MavenPublication) {
            groupId 'net.measurementlab.ndt7.android'
            artifactId 'libndt7'
            version mavenVersion
            artifact("$buildDir/outputs/aar/libndt7-release.aar")
        }

        debug(MavenPublication) {
            groupId 'net.measurementlab.ndt7.android'
            artifactId 'libndt7-debug'
            version mavenVersion
            artifact("$buildDir/outputs/aar/libndt7-debug.aar")
        }
    }

    repositories {
        maven {
            url "s3://mirror-artifacts/"
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
        mavenLocal()
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 30
        versionCode mavenVersionCode
        versionName mavenVersion
        consumerProguardFiles "proguard-rules.pro"

        // AGP 4.1+ doesn't add VERSION_NAME or VERSION_CODE to aars by default anymore
        def version =  '\"' + versionName.toString() +'\"'
        buildConfigField 'String', 'NDT7_ANDROID_VERSION_NAME', version

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.code.gson:gson:$gson_version"

    androidTestImplementation "androidx.test:runner:1.3.0"
}

repositories {
    mavenCentral()
}
